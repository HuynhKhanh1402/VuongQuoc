plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.3'
}

group = 'dev.khanh.plugin'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = 'https://maven.enginehub.org/repo/'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT")
    compileOnly("org.projectlombok:lombok:1.18.32")
    compileOnly("me.clip:placeholderapi:2.11.6");
    compileOnly("com.sk89q.worldedit:worldedit-bukkit:7.2.14");
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.0");

    implementation("com.github.HuynhKhanh1402:KPlugin:1.11.1")
    implementation("com.zaxxer:HikariCP:6.1.0")

    annotationProcessor("org.projectlombok:lombok:1.18.32")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

shadowJar {
    setArchiveClassifier("")

    relocate("dev.khanh.plugin.kplugin", "dev.khanh.plugin.vuongquoc.shaded.kplugin")
    relocate("com.zaxxer.hikari", "dev.khanh.plugin.shaded.hikari")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
